# SPDX-License-Identifier: AGPL-3.0-or-later

import os
import glob

from SCons.Environment import Environment
from SCons.Script import Export


Import('TARGET_ENVIRONMENT')
TARGET_ENVIRONMENT: Environment

# Configure musl library and include paths for all usermode modules
musl_include = os.path.join(str(TARGET_ENVIRONMENT['TOOLCHAIN_PREFIX']), 'include')
musl_lib = os.path.join(str(TARGET_ENVIRONMENT['TOOLCHAIN_PREFIX']), 'lib')

# Create a usermode environment with musl support
usermode_env = TARGET_ENVIRONMENT.Clone()
usermode_env.Append(
    CPATH=[musl_include],
    CPPPATH=[musl_include],
    LINKFLAGS=[
        '-L', musl_lib,
        '-static',  # Default to static linking for usermode
        '-lc',  # Link musl libc
    ]
)

# Export for use in sub-SConscripts
Export('usermode_env')
Export('musl_include')
Export('musl_lib')

# Build libmath and libdisplay libraries first so core can link them statically
SConscript('libmath/SConscript', variant_dir='libmath_build', duplicate=0)
SConscript('sh/SConscript', variant_dir='sh_build', duplicate=0)

# Import libmath library and sh executable
Import('libmath_module')
Import('sh_module')

# Separate user libraries (shared objects) and applications (executables)
user_libraries = [libmath_module]
user_applications = [sh_module]

# Import any module libraries that were built
for module_dir in glob.glob('*/'):
    if module_dir.startswith('core'):
        continue
    if module_dir in ['libmath/', 'sh/']:
        continue
    if not os.path.isdir(module_dir):
        continue
    
    has_sources = (
        len(glob.glob(f'{module_dir}/*.c')) > 0 or
        len(glob.glob(f'{module_dir}/*.cpp')) > 0 or
        len(glob.glob(f'{module_dir}/*.rs')) > 0 or
        len(glob.glob(f'{module_dir}/**/*.c', recursive=True)) > 0 or
        len(glob.glob(f'{module_dir}/**/*.cpp', recursive=True)) > 0 or
        len(glob.glob(f'{module_dir}/**/*.rs', recursive=True)) > 0
    )
    
    if has_sources:
        module_name = module_dir.rstrip('/')
        try:
            Import(f'{module_name}_module')
            module = locals()[f'{module_name}_module']
            # Check if it's a shared library (.so) or executable
            module_path = str(module)
            if module_path.endswith('.so'):
                user_libraries.append(module)
            else:
                user_applications.append(module)
        except:
            pass

Export('user_libraries')
Export('user_applications')
